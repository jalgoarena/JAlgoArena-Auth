buildscript {
    ext.fasterxml_version = '2.9.5'
}

plugins {
    id "jacoco"
    id "org.jetbrains.kotlin.jvm" version "1.2.51"
    id 'org.springframework.boot' version '2.0.1.RELEASE'
}

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

apply from: "$rootDir/gradle/versioning.gradle"

group = 'jalgoarena'
description = """JAlgoArena Auth Server"""

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

jar {
    baseName = 'jalgoarena-auth'
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Finchley.RELEASE'
    }
}

dependencies {
    compile "com.fasterxml.jackson.core:jackson-databind:$fasterxml_version",
            "com.fasterxml.jackson.core:jackson-core:$fasterxml_version",
            "com.fasterxml.jackson.core:jackson-annotations:$fasterxml_version",
            "com.fasterxml.jackson.module:jackson-module-kotlin:$fasterxml_version",
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-actuator",
            "org.springframework.boot:spring-boot-starter-security",
            'org.springframework.security:spring-security-config',
            "org.jetbrains.kotlin:kotlin-stdlib",
            "org.jetbrains.kotlin:kotlin-reflect",
            "io.jsonwebtoken:jjwt:0.7.0",
            'net.logstash.logback:logstash-logback-encoder:5.1',
            'joda-time:joda-time',
            'org.springframework.boot:spring-boot-starter-data-jpa',
            'org.postgresql:postgresql',
            'com.github.ben-manes.caffeine:caffeine'

    compileOnly "org.springframework.boot:spring-boot-configuration-processor"

    testCompile 'junit:junit:4.12',
            "org.springframework.boot:spring-boot-starter-test",
            "com.h2database:h2"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
}

apply plugin: 'jacoco'

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

defaultTasks 'clean', 'bootJar'

task stage {
    dependsOn 'bootJar'
}

task releaseZip(type: Zip, dependsOn: 'bootJar') {
    from('build/libs/') {
        include('*.jar')
    }
    from 'run.sh'
    archiveName "JAlgoArena-Auth-${version}.zip"
    destinationDir(file('build/libs'))
}

task buildDockerImage(type: Exec, dependsOn: 'bootJar') {
    commandLine 'docker',  'build', '-t', "jalgoarena/auth:${version}", '.'
}

task pushDockerImages(type: Exec, dependsOn: 'buildDockerImage') {
    commandLine 'docker',  'push', "jalgoarena/auth:${version}"
}